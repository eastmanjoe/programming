'################################################
' Test to parse Status.IPInfo
'
'################################################

Public test_ver As String = "4.01.00"


'## Configuration ###############################
PipeLineMode


'################################################

'## Declarations ################################
Const SP = Chr(32)
Const TB = Chr(9)
Const CR = Chr(13)
Const LF = Chr(10)
Const CRLF = CR & LF

Public pnl_logger_ip_info(8) As String * 42
Alias pnl_logger_ip_info = pnl_logger_ip_info_mac, pnl_logger_ip_info_ipv4, _
  pnl_logger_ip_info_ipv6, pnl_logger_ip_info_netmask, pnl_logger_ip_info_gateway, pnl_logger_ip_info_dns1, _
  pnl_logger_ip_info_dns2, pnl_logger_ip_info_dns3
'################################################

'## Data Tables #################################
' DataTable(fifteenMin, TRUE, -1)
'   DataInterval(0, 15, MIN, 0)
'   #If LoggerType = CR1000
'   CardOut(0, -1)
'   #EndIf

' EndTable 'fifteenMin
'################################################

'## Functions ###################################
Function parseIpInfo()
  'Pre-OS28
    'Data in CR800 Status.IPInfo =
      ' CSIO(1) MAC: 00d02c0209b0
      ' CSIO(1) IP: 10.11.50.26
      ' CSIO(1) mask: 255.255.0.0
      ' CSIO(1) gw: 10.11.1.1
      ' dns: 10.11.10.11
      ' dns: 10.11.10.12
    'Data in CR1000 Status.IPInfo =
      ' eth MAC: 00d02c044e5b
      ' eth IP: 192.168.13.100
      ' eth mask: 255.255.255.0
      ' eth gw: 192.168.13.2
      ' dns: 8.8.8.8
      ' dns: 8.8.4.4

  'Post-OS28
    'Data in CR800 Status.IPInfo =
      ' csio(1) is default network
      ' csio(1) mac: 00d02c0209b0
      ' csio(1) ip: 10.11.50.26
      ' csio(1) mask: 255.255.0.0
      ' csio(1) gw: 10.11.1.1
      ' csio(1) ipv6 local: fe80::2d0:2cff:fe02:9b0
      ' dns: 8.8.8.8
      ' dns: 208.67.22
    'Data in CR1000 Status.IPInfo =
      ' eth is default network
      ' eth mac: 00d02c044e5b
      ' eth ip: 10.11.50.10
      ' eth mask: 255.255.0.0
      ' eth gw: 10.11.1.1
      ' eth ipv6 local: fe80::2d0:2cff:fe04:4e5b
      ' dns: 8.8.8.8
      ' dns: 10.11.10.12
      ' dns: 208.67.222.222'
  'IP Address length is between 7 and 15 characters

  Dim counter As Long
  Dim buffer_str(9) As String * 44
  Dim line_buffer As String * 255

  Erase(buffer_str)

  'parse Status.IPInfo to get current settings
  line_buffer = LowerCase(Status.IPInfo)
  line_buffer = Replace(line_buffer, SP, "")
  line_buffer = Replace(line_buffer, CRLF, ",")

  SplitStr(buffer_str(), line_buffer, ",", 9, 5)

  #If LoggerType = CR1000 Then
    pnl_logger_ip_info_ipv4 = Status.IPAddressEth
    pnl_logger_ip_info_netmask = Status.IPMaskEth
    pnl_logger_ip_info_gateway = Status.IPGateway
  #ElseIf LoggerType = CR800 Then
    pnl_logger_ip_info_ipv4 = Status.IPAddressCSIO(1)
    pnl_logger_ip_info_netmask = Status.IPMaskCSIO(1)
    pnl_logger_ip_info_gateway = Status.IPGatewayCSIO(1)
  #EndIf

  'remove the text prior to the semicolon
  buffer_str(2) = Right(buffer_str(2), Len(buffer_str(2)) - InStr(1, buffer_str(2), ":", 2))
  buffer_str(2) = UpperCase(buffer_str(2))

  'change mac address format from 00d02c0209b0 to 00:D0:2C:02:09:B0
  For counter = 1 To Len(buffer_str(2)) Step 2
    pnl_logger_ip_info_mac &= Mid(buffer_str(2), counter, 2)

    If counter < 11 Then pnl_logger_ip_info_mac &= ":"
  Next

  pnl_logger_ip_info_ipv6 = Right(buffer_str(6), Len(buffer_str(6)) - InStr(1, buffer_str(6), ":", 2))
  pnl_logger_ip_info_dns1 = Right(buffer_str(7), Len(buffer_str(7)) - InStr(1, buffer_str(7), ":", 2))
  pnl_logger_ip_info_dns2 = Right(buffer_str(8), Len(buffer_str(8)) - InStr(1, buffer_str(8), ":", 2))
  pnl_logger_ip_info_dns3 = Right(buffer_str(9), Len(buffer_str(9)) - InStr(1, buffer_str(9), ":", 2))
EndFunction 'parseIpInfo
'################################################


'## Main Program ################################
BeginProg
  Scan (10,Sec,3,0)
    parseIpInfo()
  NextScan
EndProg
'################################################
